/*
 * This software is available under Apache License
 * Copyright (c) 2020
 */

// main project settings:
rootProject.name = "pixel"

// include settings:
var includeAndroid = false

// modules:
include "modules/commons",
        "modules/math",
        "modules/graphics",
        "modules/content",
        "modules/core",
        "modules/pipeline"

// platform modules:
include "platform/desktop"
if (includeAndroid && System.getenv("ANDROID_HOME") != null) {
    include "platform/android"
}

// extensions:
include "extensions/ext-log4j2",
        "extensions/ext-ecs",
        "extensions/ext-ecs-extra",
        "extensions/ext-tween"

// demos:
if (System.properties["PIXEL_RELEASE"] == null) {
    // desktop:
    include "demos/desktop/demo-desktop-learning",
            "demos/desktop/demo-desktop-concept",
            "demos/desktop/demo-desktop-imgui"

    // android:
    if (includeAndroid && System.getenv("ANDROID_HOME") != null) {
        include "demos/android/demo-android-learning"
    }
}

// bootstrap module names:
rootProject.children.each {
    def parts = it.name.split("/")
    def prefix = rootProject.name + "-"
    it.name = prefix + parts.last()
}
