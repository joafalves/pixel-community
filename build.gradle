/*
 * This software is available under Apache License
 * Copyright (c) 2020
 */
import org.gradle.internal.os.OperatingSystem

// Pixel Version:
version = System.properties["PIXEL_RELEASE"]
if (version == null || version == "unspecified") {
    version = "SNAPSHOT"
}

ext {
    dependencyMap = [
            lwjgl  : "3.3.4",
            lombok : "1.18.32",
            junit  : "5.10.2",
            mockito: "4.8.0"
    ]
}

println("Java Version: ${JavaVersion.current()}")
println("Project Version: ${version}")
println("Android Home: ${System.getenv("ANDROID_HOME")}")

buildscript {
    ext {
        kotlin_version = '1.9.20'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    version = rootProject.version
    ext {
        namespace = "org.pixel"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }
}

subprojects { project ->
    apply plugin: "maven-publish"
    apply plugin: "signing"

    publishing {
        repositories {
            maven {
                url = project.version.endsWith('SNAPSHOT') ?
                        "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                        "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USERNAME")
                    password = findProperty("sonatypePassword") ?: System.getenv("SONATYPE_PASSWORD")
                }
            }
        }
    }

    tasks.withType(Javadoc).tap {
        configureEach {
            failOnError false
            options.addStringOption("Xdoclint:none", "-quiet")
            options.addStringOption("encoding", "UTF-8")
            options.addStringOption("charSet", "UTF-8")
        }
    }

    project.plugins.withType(JavaPlugin).whenPluginAdded {
        dependencies {
            // lombok
            compileOnly "org.projectlombok:lombok:${rootProject.ext.dependencyMap["lombok"]}"
            annotationProcessor "org.projectlombok:lombok:${rootProject.ext.dependencyMap["lombok"]}"

            // test:
            testImplementation "org.mockito:mockito-core:${rootProject.ext.dependencyMap["mockito"]}"
            testImplementation "org.junit.jupiter:junit-jupiter-api:${rootProject.ext.dependencyMap["junit"]}"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${rootProject.ext.dependencyMap["junit"]}"
        }

        java {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }

        test {
            useJUnitPlatform()
        }

        jar {
            manifest {
                attributes(
                        "Implementation-Title": jar.getArchiveBaseName(),
                        "Implementation-Version": getArchiveVersion()
                )
            }
        }

        tasks.register('sourceJar', Jar) {
            archiveClassifier.set('sources')
            from sourceSets.main.allJava
        }

        tasks.register('javadocJar', Jar) {
            dependsOn javadoc
            archiveClassifier.set('javadoc')
            from javadoc.destinationDir
        }

        tasks.withType(AbstractPublishToMaven).configureEach {
            // only publish modules with "mavenPublish" set to "true"
            it.enabled = (project.ext.has("mavenPublish") && project.ext.get("mavenPublish"))
        }

        artifacts {
            archives jar
            archives sourceJar
            archives javadocJar
        }

        signing {
            if (rootProject.hasProperty("signing.keyId")) {
                sign(publishing.publications) // sign everything to be published
            }
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    artifact(sourceJar) {
                        classifier = "sources"
                    }

                    artifact(javadocJar) {
                        classifier = "javadoc"
                    }

                    groupId = "io.github.joafalves"
                    artifactId = project.name
                    version = rootProject.version

                    pom {
                        name = "Pixel 2D Game Framework"
                        description = "Modular Java/Kotlin 2D Game Framework."
                        url = "https://github.com/joafalves/pixel-community"
                        licenses {
                            license {
                                name = "The Apache License, Version 2.0"
                                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            }
                        }
                        developers {
                            developer {
                                id = "jfalves"
                                name = "Jo√£o Alves"
                                email = "joao.cpp.sca@gmail.com"
                            }
                        }
                        scm {
                            connection = "scm:git:git://github.com/joafalves/pixel-community.git"
                            developerConnection = "scm:git:ssh://github.com/joafalves/pixel-community.git"
                            url = "https://github.com/joafalves/pixel-community"
                        }
                    }
                }
            }
            repositories {
                if (rootProject.hasProperty("sonatypeUsername") && rootProject.hasProperty("sonatypePassword")) {
                    maven {
                        url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                        credentials {
                            username sonatypeUsername
                            password sonatypePassword
                        }
                    }
                }
            }
        }
    }
}
