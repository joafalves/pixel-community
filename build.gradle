/*
 * This software is available under Apache License
 * Copyright (c) 2020
 */
import org.gradle.internal.os.OperatingSystem

// Pixel Version:
version = System.properties["PIXEL_RELEASE"]
if (version == null || version == "unspecified") {
    version = "SNAPSHOT"
}

ext {
    dependencyMap = [
            lwjgl  : "3.3.3",
            lombok : "1.18.32",
            junit  : "5.10.2",
            mockito: "4.8.0"
    ]
    buildTargetList = determineBuildTargets()
}

static ArrayList<String> determineBuildTargets() {
    // TODO: update this when android platform is fully supported
    def buildTarget = System.properties["BUILD_TARGET"]
    if (buildTarget == "all") {
        return ["natives-windows", "natives-windows-x86", "natives-windows-arm64",
                "natives-macos", "natives-macos-arm64",
                "natives-linux", "natives-linux-arm64", "natives-linux-arm32"]
    } else if (buildTarget != null) {
        return [buildTarget]
    } else {
        return [getCurrentClassifier()]
    }
}

println("Java Version: ${JavaVersion.current()}")
println("Project Version: ${version}")
println("Build Targets: ${project.ext.buildTargetList}")
println("Android Home: ${System.getenv("ANDROID_HOME")}")

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
    }
}

allprojects {
    version = rootProject.version
    ext {
        namespace = "org.pixel"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }
}

subprojects { project ->
    apply plugin: "maven-publish"

    publishing {
        repositories {
            maven {
                url = project.version.endsWith('SNAPSHOT') ?
                        "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                        "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = findProperty("sonatypeUsername") ?: System.getenv("SONATYPE_USERNAME")
                    password = findProperty("sonatypePassword") ?: System.getenv("SONATYPE_PASSWORD")
                }
            }
        }
    }

    tasks.withType(Javadoc).tap {
        configureEach {
            failOnError false
            options.addStringOption("Xdoclint:none", "-quiet")
            options.addStringOption("encoding", "UTF-8")
            options.addStringOption("charSet", "UTF-8")
        }
    }

    project.plugins.withType(JavaPlugin).whenPluginAdded {
        dependencies {
            // lombok
            compileOnly "org.projectlombok:lombok:${rootProject.ext.dependencyMap["lombok"]}"
            annotationProcessor "org.projectlombok:lombok:${rootProject.ext.dependencyMap["lombok"]}"

            // test:
            testImplementation "org.mockito:mockito-core:${rootProject.ext.dependencyMap["mockito"]}"
            testImplementation "org.junit.jupiter:junit-jupiter-api:${rootProject.ext.dependencyMap["junit"]}"
            testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${rootProject.ext.dependencyMap["junit"]}"
        }

        test {
            useJUnitPlatform()
        }

        jar {
            manifest {
                attributes(
                        "Implementation-Title": jar.getArchiveBaseName(),
                        "Implementation-Version": getArchiveVersion()
                )
            }
        }

        tasks.register('sourceJar', Jar) {
            archiveClassifier.set('sources')
            from sourceSets.main.allJava
        }

        tasks.register('javadocJar', Jar) {
            dependsOn javadoc
            archiveClassifier.set('javadoc')
            from javadoc.destinationDir
        }

        artifacts {
            archives jar
            archives sourceJar
            archives javadocJar
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    groupId = project.namespace
                    artifactId = 'your-java-library'
                    version = '1.0.0'

                    // Additional POM information goes here
                }
            }
        }
    }
}

static def getCurrentClassifier() {
    switch (OperatingSystem.current()) {
        case OperatingSystem.LINUX:
            def osArch = System.getProperty("os.arch")
            return osArch.startsWith("arm") || osArch.startsWith("aarch64")
                    ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                    : "natives-linux"

        case OperatingSystem.MAC_OS:
            return System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"

        case OperatingSystem.WINDOWS:
            def osArch = System.getProperty("os.arch")
            return osArch.contains("64")
                    ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                    : "natives-windows-x86"
    }
}
